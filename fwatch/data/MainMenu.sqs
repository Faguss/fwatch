; Script supplied with Fwatch v1.13
; Handles extra main menu options - Mod Selector, Master Server Changer, Addon Downloader
; Executed from Resource.cpp

;Changelog:
; 1.16
; uses Fwatch 1.16
; added Game Schedule feature
; added "movetop" and "tolerance" resource instructions

; 1.15
; uses Fwatch 1.15
; no input limit

	GS_FWATCH_MY_VERSION = [2023,9,17,0,18,24,24,830,120,FALSE]
	GS_MY_VERSION        = 0.61
	GS_DEFAULT_URL       = [["http://ofp-faguss.com/schedule/sqf.php","http://faguss.paradoxstudio.uk/schedule/sqfproxy.php"]]

	; Won't work with older version
	_fwatch_version = call loadFile ":info version"
	? _fwatch_version < 1.16 : titleText ["Requires Fwatch 1.16. Please update your version.","PLAIN DOWN",1]; exit

	_fwatch_test_version = 0
	_fwatch_version      = call loadFile ":info version extended"
	IS_CWA               = _fwatch_version select 1
	_my_game_version     = _fwatch_version select 4
	? _fwatch_test_version < 3 : titleText ["New test version of Fwatch 1.16 required.\n\nPlease update your version.\n\nofp-faguss.com/fwatch/116test","PLAIN",1.5]; exit
	
	? "download" in _this                : goto "DownloadImageThread"
	? MAINMENU_INPUT || ctrlVisible 6900 : exit
	? ctrlVisible 6657                   : if (!(lbValue[6657,0] in [1,4])) then {LOADMOD_MODSELECTOR=true} else {closeDialog 0}; exit

	; Initialize functions
	call preProcessFile "..\fwatch\data\MainMenu_fnc.sqf"
	["FLIB_DATEDIFF","FLIB_DATEDIFFDAY","FLIB_FORMATDATE","FLIB_MODIFYDATE","FLIB_CURRENTLANG","FLIB_FORMAT"] call preProcessFile "..\fwatch\data\InitFLib.sqf"
	call FUNCTION_STRINGTABLE
	0.03 exec "..\fwatch\data\InputMulti.sqs"
	@Format ["%1",count FWATCH_INPUT_MULTI] != "scalar"
	
	; Remove invalid databases
	{_ok=call loadFile Format ["\:IGSE DB  ""file:..\fwatch\tmp\schedule\%1"" verify:1",_x]; if ((_ok select 1) in [280,282,283,284,285,286,287,288,289]) then {loadFile Format ["\:IGSE NEW  ""file:..\fwatch\tmp\schedule\%1""  mode:delete",_x]}} forEach ["schedule.bin", "params.bin", "schedulelogo.bin"]
	
	_silent_mode = "silent" in _this
	_ok          = if (!_silent_mode) then {createDialog "MAINMENU_MODSLIST"} else {true}
	? !_ok : titleText [MAINMENU_STR select 0,"PLAIN DOWN",1]; exit

	; Hide info window
	_info_window = [6460, 6461, 6462, 6463,   6470, 6471, 6472,   6480, 6481, 6482,   6490, 6491, 6492,   6500, 6501, 6502,  6510, 6511,   6520, 6521,   6530, 6531,   6540, 6541,   6550,6551,  6464]
	"ctrlShow [_x,false]" forEach _info_window
	ctrlEnable [6463, false]
	ctrlSetText [6450, MAINMENU_STR select 2]
	ctrlSetText [6451, MAINMENU_STR select 3]
	
	; Check resource.cpp version
	_my_cpp_version       = 0
	_required_cpp_version = 0.5
	? ctrlVisible 6465 : if (ctrlText 6465 != "") then {_my_cpp_version=call (ctrlText 6465)}
	? !_silent_mode && _my_cpp_version != _required_cpp_version : if (count (call loadFile ":mem modlist") == 0) then {goto "DisplayUpdateInformation"} else {titleText [MAINMENU_STR select 1,"PLAIN DOWN",1]}
	
	; Set variables for switching sections
	LOADMOD_MODSELECTOR  = false
	LOADMOD_MASTERSERVER = false
	LOADMOD_SCHEDULE     = false
	
	? _silent_mode : goto "InitLocalVars"
	
	; Listbox position
	_x         = (_this select 0)
	_y         = (_this select 1)
	_w         = _x + (_this select 2)
	_h         = _y + (_this select 3)
	_tolerance = 0.05
	_mx        = (FWATCH_INPUT_MULTI select 3) select 2
	_my        = (FWATCH_INPUT_MULTI select 3) select 3
	_m_moved   = false

	; Execute additional instructions from the resource.cpp
	? count _this <= 4 : goto "SkipResourceInstructions"
	
		_i = 4
		#ResourceInstructions
		_instruction = (_this select _i)
		? _instruction select 0 == "moverel"   : loadFile Format [":mem setcursor %1 %2", _mx+(_instruction select 1),_my+(_instruction select 2)]; _m_moved=true
		? _instruction select 0 == "moveabs"   : loadFile Format [":mem setcursor %1 %2", (_instruction select 1),(_instruction select 2)]; _m_moved=true
		? _instruction select 0 == "movetop"   : loadFile Format [":mem setcursor %1 %2", _x++0.05, _y+0.05]; _m_moved=true
		? _instruction select 0 == "extendx"   : _x=_x+(_instruction select 1); _w=_w+abs (_instruction select 1)
		? _instruction select 0 == "extendy"   : _y=_y+(_instruction select 1); _h=_h+abs (_instruction select 1)
		? _instruction select 0 == "extendw"   : _w=_w+(_instruction select 1)
		? _instruction select 0 == "extendh"   : _h=_h+(_instruction select 1)
		? _instruction select 0 == "tolerance" : _tolerance=_instruction select 1
		? _instruction select 0 == "lowercase" : ctrlSetText [6450, MAINMENU_STR select 78]; ctrlSetText [6451, MAINMENU_STR select 79]
		? _i < count _this-1                   : _i=_i+1; goto "ResourceInstructions"
		#SkipResourceInstructions

	#InitLocalVars
	_cur                 = -1
	_lastCur             = -1
	_launch_queue        = []
	_pressed             = ""
	_install_status      = -1
	_install_timing      = 0
	_install_hanged      = false
	_install_madechoice  = false
	_jump_to_server      = ""
	_timeout             = 0
	_color_red           = [1,0,0,0.5]
	_color_pink          = [1,0.8,0.9,0.5]
	_color_fireenginered = [246/255, 40/255 , 23/255 , 1]
	_color_coral         = [255/255, 127/255, 80/255 , 1]
	_color_sandybrown    = [238/255, 154/255, 77/255 , 1]
	_color_gingerbrown   = [201/255, 190/255, 98/255 , 1]
	_color_yellowgreen   = [82/255 , 208/255, 23/255 , 1]
	_color_sand          = [194/255, 178/255, 128/255, 1]
	_color_cottoncandy   = [252/255, 223/255, 255/255, 1]
	_mod_name            = ""
	_mod_type            = 0
	_mod_version         = 0
	_mod_forcename       = false
	_mod_size            = ""
	_mod_sizearray       = [0,0,0]
	_mod_is_mp           = true
	_mod_addedby         = ""
	_mod_description     = ""
	_mod_website         = ""
	_mod_logo            = ""
	_mod_logohash        = ""
    _server_uniqueid     = "mod"
	_mod_shortcuts_names = []
	_mod_shortcuts_data  = []
	_current_shortcut_id = -1
	_scheduled_events    = []
    
	if (!_m_moved && (_mx<_x || _mx>_w || _my<_y || _my>_h)) then {loadFile Format [":mem setcursor %1 %2", _w, _y]; _m_moved=nil}
	goto "ModLauncher_Display"
	

	
	
	; Loop checking mouse cursor position -------------------------------------------------------------------------------
	#ResetLMB
	? _silent_mode : exit
	_LMBtime = 0
	_release = false
	
	#MainLoop
	~0.03
	? !ctrlVisible 6657    : exit
	? LOADMOD_MODSELECTOR  : LOADMOD_MODSELECTOR =false; "ctrlShow [_x,false]" forEach _info_window; _lastCur=-1; goto "ModLauncher_Display"
	? LOADMOD_MASTERSERVER : LOADMOD_MASTERSERVER=false; "ctrlShow [_x,false]" forEach _info_window; _lastCur=1 ; goto "MasterServer"
	? LOADMOD_SCHEDULE     : LOADMOD_SCHEDULE    =false; goto "Schedule"
	
	; If selection changed in the listbox
	_cur = lbCurSel 6657
	? _cur==-1  ||  _cur==_lastCur : goto "SkipSelectionCheck"
	
		_name    = lbText [6657, _cur]
		_data    = lbData [6657, _cur]
		_value   = lbValue [6657, _cur]
		_lastCur = _cur
		
		? _value in [2,3,12,13] : [_data, "local"] call FUNCTION_SHOW_MOD_INFO
		? _value == 6           : [_data, "global"] call FUNCTION_SHOW_MOD_INFO
		? _value == 15          : goto "ModLauncher_Shortcut_Preview"
		? _value == 201         : if (ctrlVisible 6463) then {goto "DisplayServerStatus"} else {goto "DisplayServer"}
		#SkipSelectionCheck
	
	_mx = (FWATCH_INPUT_MULTI select 3) select 2
	_my = (FWATCH_INPUT_MULTI select 3) select 3
	
	; Read input to check double-click
	_keys  = FWATCH_INPUT_MULTI select 0
	_LMB   = false
	_shift = "LSHIFT" in _keys || "RSHIFT" in _keys
	
	? _pressed in _keys : goto "MainLoop"
	_pressed = ""
	
	? "LEFT" in _keys    : _pressed="LEFT"; _found=-1; _i=-1; while "_i=_i+1; _i<lbSize 6657 && _found<0" do "if (lbValue [6657,_i] in [207,208,210,212]) then {_found=_i}"; if (_found>=0) then {_name=lbText [6657, _found]; _data=lbData [6657, _found]; _value=lbValue [6657, _found]; goto "Option"} else {closeDialog 0; exit}
	? "RIGHT" in _keys   : _pressed="RIGHT"; goto "Option"
	? "RBUTTON" in _keys : _pressed="RBUTTON"; goto "OptionRightMouseClick"
	? "SPACE" in _keys   : _pressed="SPACE"; goto "OptionRightMouseClick"
	? "LBUTTON" in _keys : _LMB=true; _pressed="LBUTTON"
		
		? _LMBtime>0  &&  _time-_LMBtime>0.35 : goto "ResetLMB"
		? _LMBtime>0  &&  _LMB                : _release=true; goto "MainLoop"
		? _release    &&  !_LMB  &&  _cur>=0  : goto "Option"
		? _LMBtime==0 &&  _LMB                : _LMBtime=_time
	
	? _install_status >= 0 : if (_time+0.1>_install_timing) then {_install_timing=_time; goto "InstallWait"}
	goto "MainLoop"


	
	
	; User double-clicked on the listbox row ----------------------------------------------------------------------------
	#Option
	_lastCur = -1

	; Modfolders options
	? _value == 1  : loadFile ":restart client"; closeDialog 0; exit
	? _value == 2  : goto "ModLauncher_AddToQueue"
	? _value == 3  : if (_shift) then {goto "ModLauncher_RemoveFromQueue"} else {goto "ModLauncher_RestartGame"}
	? _value == 4  : _mode="mods"; _server_uniqueid="mod"; goto "DownloadDatabase"
	? _value == 5  : goto "ModLauncher_Display" 
	? _value == 6  : goto "DownloadMods_QueueForInstall"
	? _value == 7  : _name=_data; goto "ModLauncher_AddToQueue"
	? _value == 8  : _name=_data; if (_name in _launch_queue) then {_launch_queue=_launch_queue-[_name]; lbSetColor [6657,_cur,[1,1,1, 0.65]]} else {_launch_queue=_launch_queue+[_name]; lbSetColor [6657, _cur, _color_red]}; goto "ModLauncher_ShowQueue"
	? _value == 9  : goto "DownloadMods_ShowOptions"
	? _value == 10 : _shift=true; goto "DownloadMods_QueueForInstall"
	? _value == 11 : _Label="ModLauncher_Shortcut_Create"; _Input=""; _Input_Note=MAINMENU_STR select 110; _current_shortcut_id=-1; goto "INPUT"
	? _value == 12 : _mods_for_new_shortcut=_mods_for_new_shortcut+[_name]; lbSetValue [6657, _cur, 13]; lbSetColor [6657, _cur, _color_red]; _mods_for_new_shortcut call FUNCTION_DISPLAY_MOD_QUEUE
	? _value == 13 : _mods_for_new_shortcut=_mods_for_new_shortcut-[_name]; lbSetValue [6657, _cur, 12]; lbSetColor [6657,_cur,[1,1,1, 0.65]]; _mods_for_new_shortcut call FUNCTION_DISPLAY_MOD_QUEUE
	? _value == 14 : goto "ModLauncher_Shortcut_Save"
	? _value == 15 : goto "ModLauncher_Shortcut_Launch"
	? _value == 16 : goto "ModLauncher_Shortcut_Launch"
	? _value == 17 : goto "ModLauncher_Shortcut_Delete"
	? _value == 18 : _Label="ModLauncher_Shortcut_Edit"; _Input=_mod_shortcuts_names select _current_shortcut_id; _Input_Note=MAINMENU_STR select 110; goto "INPUT"
	
	; Master server options
	? _value == 100 : _Label="MasterServer_ValidateInput"; _Input=""; _Input_Note=""; goto "INPUT"
	? _value == 101 : titleText [MAINMENU_STR select 4, "PLAIN DOWN", 0.1]; goto "ResetLMB"
	? _value == 102 : loadFile Format ["\:mem masterserver set %1", _name]; _Input=_name; goto "MasterServer_SaveFile"
	
	; Game schedule options
	? _value == 201 : goto "ServerOptions"
	? _value == 204 : goto "DownloadModfolders"
	? _value == 205 : loadFile Format ["\:EXE WEBSITE %1", (if (_server_uniqueid=="mod") then {_mod_website} else {_server_website})]
	? _value == 206 : goto "EditStartupParameters"
	? _value == 207 : goto "Schedule_List_Servers"
	? _value == 208 : if (_install_hanged) then {loadFile ":IGSE DB  file:..\fwatch\tmp\schedule\schedule.bin  remove:savestate"; loadFile ":IGSE NEW  mode:delete  file:..\fwatch\tmp\schedule\install_progress.sqf"; _install_status=-1; _install_hanged=false; if (_server_uniqueid=="mod") then {goto "ModLauncher_Display"} else {goto "Schedule"}} else {loadFile Format ["\:IGSE WRITE  file:..\fwatch\tmp\schedule\InstallerInstruction.txt  text:abort"]}
	? _value == 209 : loadFile Format ["\:IGSE WRITE  file:..\fwatch\tmp\schedule\InstallerInstruction.txt  text:restart"]
	? _value == 210 : goto "CloseInstaller"
	? _value == 211 : goto "CancelGameRestart"
	? _value == 212 : goto "ServerOptions"
	? _value == 213 : goto "AssignID_ForThisMod"
	? _value == 214 : goto "AssignID_NextMod"
	? _value == 215 : _Input= _access_code; _Label="SaveAccessCode"; _Input_Note=MAINMENU_STR select 5; goto "INPUT"
	? _value == 216 : _ok=call loadFile "\:RESTART CLIENT -selfupdate=true"; if (!(_ok select 0)) then {titleText [_ok select 3, "PLAIN DOWN", 1.5]} else {closeDialog 0; exit}
	? _value == 217 : _run_voice_program=true; goto "ExecuteProgram"
	? _value == 218 : _run_voice_program=false; goto "ExecuteProgram"
	? _value == 219 : loadFile Format ["\:IGSE WRITE  file:..\fwatch\tmp\schedule\InstallerInstruction.txt  text:voice"]
	? _value == 220 : _action="auto"; goto "Voice"
	? _value == 221 : _action="connect"; goto "Voice"
	? _value == 222 : if ((GS_VOICE select (_server_voice select 0)) select 1) then {loadFile Format ["\:RESTART CLIENT -evoice=%1", _server_voice select 1]} else {loadFile Format ["\:EXE WEBSITE %1", (GS_VOICE select (_server_voice select 0)) select 2]}
	? _value == 223 : lbDelete [6657, (lbSize 6657)-1]; "ctrlShow [_x,false]" forEach _info_window; goto "DownloadDatabase_Done"
	? _value == 224 : loadFile ("\:EXE WEBSITE "+([_url,_missing_mods_id,_missing_mods_myversion] call FUNCTION_BUILD_PREVIEW_LINK))
	? _value == 225 : loadFile ("\:EXE WEBSITE "+([_url,(call "_array=[]; {_array set [count _array, _x select 0]} foreach _server_modfolders; _array"),[]] call FUNCTION_BUILD_PREVIEW_LINK))
	? _value == 226 : _ok=call loadFile Format ["\:RESTART CLIENT -updateresource=%1",_my_game_version]; if (!(_ok select 0)) then {titleText [_ok select 3, "PLAIN DOWN", 1.5]} else {closeDialog 0; exit}
	? _value == 227 : closeDialog 0; exit
	? _value == 228 : _Input= _access_code_mod; _Label="SaveModAccessCode"; _Input_Note=MAINMENU_STR select 90; goto "INPUT"
	? _value == 229 : _Input= _mod_search_phrase; _Label="SaveModSearchPhrase"; _Input_Note=MAINMENU_STR select 92; goto "INPUT"
	? _value == 230 : goto "Schedule_ListServerEvents"
	? _value == 231 : _selected_event=call _data; _action="auto"; goto "Voice"
	? _value == 232 : loadFile "\:EXE WEBSITE https://ofp-faguss.com/fwatch/modmanager/details"
	? _value == 233 : loadFile Format ["\:IGSE WRITE  file:..\fwatch\tmp\schedule\InstallerInstruction.txt  text:retry"]; lbClear 6657; lbSetCurSel [6657,-1]; _install_madechoice=true
	? _value == 234 : loadFile Format ["\:IGSE WRITE  file:..\fwatch\tmp\schedule\InstallerInstruction.txt  text:pause"]
	? _value == 235 : loadFile Format ["\:IGSE WRITE  file:..\fwatch\tmp\schedule\InstallerInstruction.txt  text:resume"]
	goto "ResetLMB"
	
	#OptionRightMouseClick
	? _value in [2,3,6] : goto "ModLauncher_ShowOptions"
	? _value == 15      : goto "ModLauncher_Shortcut_Options"
	? _value == 201     : if (!ctrlVisible 6463) then {goto "DisplayServerStatus"} else {goto "DisplayServer"}
	goto "ResetLMB"


	

	
	
	
	; Mod launcher feature ==============================================================================================
	#ModLauncher_Display
	buttonSetAction [6450, "LOADMOD_SCHEDULE=true"]
	buttonSetAction [6451, "LOADMOD_MASTERSERVER=true"]

	; Obtain lists of directories
	FWATCH_MODLIST     = []
	FWATCH_MODLISTID   = []
	FWATCH_MODLISTCFG  = []
	FWATCH_CUSTOMFILE  = "";
	FWATCH_CUSTOMSIZE  = [];
	FWATCH_MODLISTHASH = "";
	FWATCH_USERNAME    = "";
	FWATCH_CURRMOD     = call loadFile ":mem modlist"
	call FUNCTION_REFRESH_MODLIST
	
	? _silent_mode : _mode="mods"; goto "DownloadDatabase"

	"ctrlShow [_x, false] " forEach [6301, 6311]
	"ctrlSetText [_x,""""]" forEach [6300, 6301]
	"ctrlShow [_x,false]"   forEach _info_window

	lbClear 6657
	lbSetCurSel [6657,-1]
	? count FWATCH_CURRMOD > 0 : [MAINMENU_STR select 6, 1, _color_gingerbrown] call FUNCTION_LBADD
	[MAINMENU_STR select 82, 4, _color_gingerbrown] call FUNCTION_LBADD
	
	call loadFile "\:IGSE DB  file:..\fwatch\idb\modshortcuts.bin read:all"
	_i=0; {_index=[Format["[%1]", _x], 15, _color_sandybrown] call FUNCTION_LBADD; lbSetData [6657,_index,Format["%1",_i]]; _i=_i+1} forEach _mod_shortcuts_names

	_i=0; FUNCTION_ADD_MOD_TO_LISTBOX forEach FWATCH_MODLIST
	
	[MAINMENU_STR select 109, 11, _color_gingerbrown] call FUNCTION_LBADD
	[MAINMENU_STR select 30, 232, _color_gingerbrown] call FUNCTION_LBADD
	
	? Format ["%1",_last_selected]!="scalar bool array string 0xfcffffef" : lbSetCurSel [6657, _last_selected+9]; lbSetCurSel [6657, _last_selected]; _last_selected=nil
	goto "ResetLMB"


	#ModLauncher_AddToQueue
	_launch_queue = _launch_queue + [_name]
	? !_shift : goto "ModLauncher_RestartGame"
	lbSetValue [6657, _cur, 3]
	lbSetColor [6657, _cur, _color_red]
	_launch_queue call FUNCTION_DISPLAY_MOD_QUEUE
	goto "ResetLMB"
	
	#ModLauncher_RemoveFromQueue
	_launch_queue = _launch_queue - [_name]
	lbSetValue [6657, _cur, 2]
	if ([_name,FWATCH_CURRMOD] call FUNCTION_FIND>=0) then {lbSetColor [6657,_cur,_color_pink]} else {lbSetColor [6657,_cur,[1,1,1, 0.65]]}
	_launch_queue call FUNCTION_DISPLAY_MOD_QUEUE
	goto "ResetLMB"

	#ModLauncher_RestartGame
	_i       = 0
	_modLine = ""
	"_modLine=_modLine+_x; if (_i<count _launch_queue-1) then {_modLine=_modLine+"";""}; _i=_i+1" forEach _launch_queue
	loadFile Format ["\:RESTART CLIENT -mod=%1", _modLine]
	closeDialog 0
	exit
	
	; User pressed right-click on a mod - display additional options
	#ModLauncher_ShowOptions
	_last_selected = _cur
	_lastCur = -1
	_queue_install = _value in [2,3]
	_queue_launch  = _value == 6
	
	lbClear 6657
	lbSetCurSel [6657, -1]
	ctrlSetText [6464, ""]

	? _queue_install : _index=lbAdd [6657, "[" + (localize "STR_SINGLE_PLAY") + "]"]; lbSetValue [6657, _index, 7]; lbSetData  [6657, _index, _name]
	? _queue_launch  : _index=lbAdd [6657, loadFile Format ["\:STRING REPLACE text:%1find:%2replace:",MAINMENU_STR select 11," %1"]]; lbSetValue [6657, _index, 6]; lbSetData [6657, _index, _data]
	
	_index = lbAdd [6657, MAINMENU_STR select 105]
	? _queue_install     : lbSetData [6657, _index, _name]; lbSetValue  [6657, _index, 8]; if (_name in _launch_queue) then {lbSetColor [6657, _index, _color_red]}
	? _queue_launch      : lbSetData [6657, _index, _data]; lbSetValue [6657, _index, 10]; if (_data in _missing_mods_id) then {lbSetColor [6657, _index, _color_red]}
	? _mod_website != "" : lbSetValue [6657, lbAdd [6657, MAINMENU_STR select 30], 205]
	
	lbSetValue [6657, lbAdd[6657, MAINMENU_STR select 32], (if (_queue_install) then {5} else {9})]
	goto "ResetLMB"
	
	
	; Show list of downloadable mods
	#DownloadMods_List
	_mod_search_phrase = ""
    _server_uniqueid   = "mod"
	call loadFile "\:IGSE DB  file:..\fwatch\tmp\schedule\params.bin  read:modsearch"
	
	_missing_mods_id            = []
	_missing_mods_name          = []
	_missing_mods_assign        = []
	_missing_mods_version       = []
	_missing_mods_myversion     = []
	_missing_mods_forcename     = []
	_missing_mods_sizearray     = [0,0,0]
	_missing_mods_size          = ""

	#DownloadMods_ShowOptions
	lbClear 6657
	lbSetCurSel [6657, -1]
	[MAINMENU_STR select 32, 5, _color_gingerbrown] call FUNCTION_LBADD
	[MAINMENU_STR select 91, 229, _color_gingerbrown] call FUNCTION_LBADD
	[MAINMENU_STR select 89, 228, _color_gingerbrown] call FUNCTION_LBADD
	"ctrlShow [_x,false]" forEach [6460, 6463]
	"ctrlShow [_x,false]" forEach _info_window
	
	_mod_search_category = -1
	_i=0; {if (_mod_search_phrase in _x) then {_mod_search_category=_i}; _i=_i+1} forEach MAINMENU_STR_MODCAT
	
	FUNCTION_ADD_DOWNLOADABLE_MOD_TO_LISTBOX forEach GS_MODS
	? Format ["%1",_last_selected]!="scalar bool array string 0xfcffffef" : lbSetCurSel [6657, _last_selected+9]; lbSetCurSel [6657, _last_selected]; _last_selected=nil
	goto "ResetLMB"
	
	#DownloadMods_QueueForInstall
	if (_data in _missing_mods_id) then {lbSetColor [6657,_cur,[1,1,1, 0.65]]; _index=[_data,_missing_mods_id] call FUNCTION_FIND; _missing_mods_id set [_index, objNull]; _missing_mods_name set [_index, objNull]; _missing_mods_assign set [_index, objNull]; _missing_mods_version set [_index, objNull]; _missing_mods_myversion set [_index, objNull]; _missing_mods_forcename set [_index, objNull]; _missing_mods_sizearray set [_index, objNull]; _missing_mods_id=_missing_mods_id-[objNull]; _missing_mods_name=_missing_mods_name-[objNull]; _missing_mods_assign=_missing_mods_assign-[objNull]; _missing_mods_version=_missing_mods_version-[objNull]; _missing_mods_myversion=_missing_mods_myversion-[objNull]; _missing_mods_forcename=_missing_mods_forcename-[objNull]; _missing_mods_sizearray=_missing_mods_sizearray-[objNull]} else {FUNCTION_IS_MOD_MISSING forEach [[_data,_mod_name,_mod_version,_mod_forcename,_mod_size,_mod_sizearray]]; lbSetColor [6657, _cur, _color_red]}
	? !_shift : goto "DownloadModfolders"
	titleText ["\n" + ([_missing_mods_name, ", "] call FUNCTION_ARRAY2STRING), "PLAIN DOWN", 0.25]
	goto "ResetLMB"
	
	
	#ModLauncher_Shortcut_Create
	? _Input == "" : titleText [MAINMENU_STR select 8, "PLAIN DOWN", 0.5]; goto "ModLauncher_Display"
	if ([_Input,_mod_shortcuts_names] call FUNCTION_FIND >= 0) then  {titleText [MAINMENU_STR select 9, "PLAIN DOWN", 0.5]; goto "ModLauncher_Display"}
	_mods_for_new_shortcut = []
	
	@ctrlVisible 6657
	lbClear 6657
	lbSetCurSel [6657,-1]
	"ctrlShow [_x,false]" forEach _info_window
	["["+(localize "STR_REPLY_DONE")+"]", 14, _color_gingerbrown] call FUNCTION_LBADD
	
	_i=0; FUNCTION_ADD_MOD_FOR_SHORTCUT_TO_LISTBOX forEach FWATCH_MODLIST
	titleText [MAINMENU_STR select 111, "PLAIN DOWN", 0.1]
	goto "ResetLMB"
	
	
	#ModLauncher_Shortcut_Edit
	? _Input == "" : titleText [MAINMENU_STR select 8, "PLAIN DOWN", 0.5]; goto "ResetLMB"
	if ([_Input,_mod_shortcuts_names-[_mod_shortcuts_names select _current_shortcut_id]] call FUNCTION_FIND >= 0) then {titleText [MAINMENU_STR select 9, "PLAIN DOWN", 0.5]; goto "ResetLMB"}
	_mods_for_new_shortcut = ["launch",_current_shortcut_id] call FUNCTION_MOD_SHORTCUTS
	
	@ctrlVisible 6657
	lbClear 6657
	lbSetCurSel [6657,-1]
	"ctrlShow [_x,false]" forEach _info_window
	["["+(localize "STR_REPLY_DONE")+"]", 14, _color_gingerbrown] call FUNCTION_LBADD
	
	_i=0; FUNCTION_ADD_MOD_FOR_SHORTCUT_TO_LISTBOX forEach FWATCH_MODLIST
	titleText [MAINMENU_STR select 111, "PLAIN DOWN", 0.1]
	goto "ResetLMB"
	
	
	#ModLauncher_Shortcut_Save
	? count _mods_for_new_shortcut == 0 : goto "ModLauncher_Display"
	_new_shortcut = []
	
		_i = 0
		#ModLauncher_SaveShortcut_ForEachMod
		_index = [_mods_for_new_shortcut select _i,FWATCH_MODLIST] call FUNCTION_FIND
		_new_shortcut set [count _new_shortcut, (if (((FWATCH_MODLISTID select _index) != "")) then {["id",FWATCH_MODLISTID select _index]} else {["name",FWATCH_MODLIST select _index]})]
		? _i < count _mods_for_new_shortcut-1 : _i=_i+1; goto "ModLauncher_SaveShortcut_ForEachMod"
	
	_mod_shortcuts_names set [(if (_current_shortcut_id == -1) then {count _mod_shortcuts_names} else {_current_shortcut_id}), _Input]
	_mod_shortcuts_data set [(if (_current_shortcut_id == -1) then {count _mod_shortcuts_data} else {_current_shortcut_id}), _new_shortcut]
	loadFile ("\:IGSE DB  file:..\fwatch\idb\modshortcuts.bin key:allwrite:_mod_shortcuts_names=" + (_mod_shortcuts_names call FLIB_FORMAT) + ";_mod_shortcuts_data=" + (_mod_shortcuts_data call FLIB_FORMAT) + ";")
	goto "ModLauncher_Display"
	
	
	#ModLauncher_Shortcut_Preview
	_current_shortcut_id = call _data
	"ctrlShow [_x,false]" forEach _info_window
	"ctrlShow [_x,true ]" forEach [6460, 6463]
	ctrlSetText [6463, ["preview",_current_shortcut_id] call FUNCTION_MOD_SHORTCUTS]
	ctrlShow [6464, true]
	ctrlSetText [6464, MAINMENU_STR select 104]
	goto "ResetLMB"
	
	
	#ModLauncher_Shortcut_Launch
	_launch_queue = ["launch",_current_shortcut_id] call FUNCTION_MOD_SHORTCUTS
	goto "ModLauncher_RestartGame"
	
	
	#ModLauncher_Shortcut_Options
	_last_selected = _cur
	_lastCur = -1
	lbClear 6657
	lbSetCurSel [6657, -1]
	_index = lbAdd [6657, "[" + (localize "STR_SINGLE_PLAY") + "]"]; lbSetValue [6657, _index, 16]
	_index = lbAdd [6657, "[" + (localize "STR_DISP_EDIT") + "]"]; lbSetValue [6657, _index, 18]
	_index = lbAdd [6657, "[" + (localize "STR_DISP_DELETE") + "]"]; lbSetValue [6657, _index, 17]
	lbSetValue [6657, lbAdd[6657, MAINMENU_STR select 32], 5]	
	goto "ResetLMB"
	
	
	#ModLauncher_Shortcut_Delete
	_mod_shortcuts_data set [_current_shortcut_id,"null"]
	_mod_shortcuts_names set [_current_shortcut_id,"null"]
	_mod_shortcuts_data=_mod_shortcuts_data-["null"]
	_mod_shortcuts_names=_mod_shortcuts_names-["null"]
	loadFile ("\:IGSE DB  file:..\fwatch\idb\modshortcuts.bin key:allwrite:_mod_shortcuts_names=" + (_mod_shortcuts_names call FLIB_FORMAT) + ";_mod_shortcuts_data=" + (_mod_shortcuts_data call FLIB_FORMAT) + ";")
	goto "ModLauncher_Display"
	;===================================================================================================================











	; MASTER SERVER feature =============================================================================================
	#MasterServer
	"ctrlShow [_x, false] " forEach [6301, 6311]
	"ctrlSetText [_x,""""]" forEach [6300, 6301]
	
	buttonSetAction [6450, "LOADMOD_SCHEDULE=true"]
	buttonSetAction [6451, "closeDialog 0"]
	
	; if transitioning from input dialog then need to wait for the listbox to appear
	@ctrlVisible 6657
	
	_current = (call loadFile ":MEM MASTERSERVER get") select 0
	? Format ["%1",_current] == "scalar bool array string 0xfcffffef" : titleText ["Can't read current master server", "PLAIN DOWN", 1.5]; closeDialog 0; exit
	
	lbClear 6657
	lbSetCurSel [6657, -1]
	lbSetValue  [6657, lbAdd [6657, MAINMENU_STR select 7], 100]
	lbSetValue  [6657, lbAdd [6657, (">"+_current)], 101]
	lbSetColor  [6657, 1, _color_pink]

	; Load local server list
	_servers = []
	_exist   = call loadFile "\:IGSE NEW  file:..\fwatch\idb\MasterServers.sqf  mode:check"
	? (_exist select 0) : goto "MasterServer_Display"
	
	_ok = call loadFile "\:IGSE NEW  file:..\fwatch\idb\MasterServers.sqf"
	? !(_ok select 0) : titleText [_ok select 3, "PLAIN DOWN", 1.5]; goto "ResetLMB"
	
	loadFile "\:IGSE WRITE  file:..\fwatch\idb\MasterServers.sqf  line:1  escape:n  text:[""master.ofpisnotdead.com""]"

	
	; Display from file
	#MasterServer_Display
	_servers = call loadFile "..\fwatch\idb\MasterServers.sqf"
	_save    = true
	"if (_x==_current) then {_save=false} else {lbSetValue [6657, lbAdd [6657, _x], 102]}" forEach _servers
	? !_save : goto "ResetLMB"
	
	if (loadFile Format ["\:STRING TRIM  left:1  right:1  text:%1", _current] == "") then {goto "ResetLMB"}
	_Input = _current
	goto "MasterServer_SaveFile"

	
	; Check user input
	#MasterServer_ValidateInput
	_Input = loadFile Format ["\:STRING TRIM  left:1  right:1  text:%1", _Input]
	? _Input == "" : titleText [MAINMENU_STR select 8, "PLAIN DOWN", 0.5]; goto "MasterServer"
	
	if ([_Input,_servers] call FUNCTION_FIND >= 0) then  {titleText [MAINMENU_STR select 9, "PLAIN DOWN", 0.5]; goto "MasterServer"}
	loadFile Format ["\:MEM MASTERSERVER set %1", _Input]

	
	; Save to file
	#MasterServer_SaveFile
	_index = [_Input, _servers] call FUNCTION_FIND
	? _index >= 0 : _servers set [_index, objNull]; _servers=_servers-[objNull]
	_servers = [_Input] + _servers;
	
	loadFile Format ["\:IGSE WRITE  file:..\fwatch\idb\MasterServers.sqf  open:recreate  text:%1", _servers call FLIB_FORMAT]
	goto "MasterServer"
	;===================================================================================================================










	; GAME SCHEDULE feature =============================================================================================
	; Download information
	#Schedule
	;buttonSetAction [6450, "closeDialog 0"]
	buttonSetAction [6451, "LOADMOD_MASTERSERVER=true"]
	_mode = "schedule"
	_scheduled_events = (call loadFile Format ["\:RESTART SCHEDULE mode:list"]) select 4
	goto "DownloadDatabase"

	; Display list of servers in a listbox
	#Schedule_List_Servers
	lbClear 6657
	lbSetCurSel [6657, -1]
	ctrlSetText [6463, ""]

	? _update_available    : goto "DisplayUpdateInformation"
	
	FUNCTION_ADD_SERVER_TO_LISTBOX forEach GS_SERVERS
	
	[MAINMENU_STR select 25, 215, _color_gingerbrown] call FUNCTION_LBADD
	? _lastCur>=0 : lbSetCurSel [6657, _lastCur]; _lastCur=-1
	goto "ResetLMB"



	; Display detailed information about a server
	#DisplayServer
	_server_name      	      = ""
	_server_ip        	      = ""
	_server_port		      = ""
	_server_password  	      = ""
	_server_version   	      = 0
	_server_equalModReq       = false
	_server_languages         = []
	_server_location   	      = ""
	_server_message		      = ""
	_server_website		      = ""
	_server_logo              = ""
	_server_logohash          = ""
	_server_uniqueid          = ""
	_server_voice             = []
	_server_game_times2       = []
	_server_modfolders	      = []
	_server_maxcustomfilesize = []
	_server_maxcustombytes    = ""
	_server_encrypted         = true
	_server_status            = []
	_today_game_time          = []
	
	_i=-1; while "_i=_i+1; _i<9" do {ctrlSetText [(6470+10*_i), MAINMENU_STR select (65+_i)]}
	_i=-1; while "_i=_i+1; _i<4" do {ctrlSetText [(6472+10*_i), MAINMENU_STR select (65+_i)]}

	_ok = call loadFile Format ["\:IGSE DB file:..\fwatch\tmp\schedule\dl_schedule.db read:%1", _data]

	_domain        = call loadFile Format ["\:STRING DOMAIN url:%1", _server_website]
	_versionString = if (_server_version == 0) then {""} else {Format ["%1",_server_version]}
	_too_big       = false

	_voice_name = if (count _server_voice > 0) then {(GS_VOICE select (_server_voice select 0)) select 0} else {""}

	"ctrlShow [_x,true]" forEach _info_window
	ctrlShow [6463, false]

	["logo_server", "scheduleserverlogo.bin", _server_logo, _server_uniqueid, _server_logohash, _server_name, lbCurSel 6657] call FUNCTION_DISPLAY_LOGO

	ctrlSetText [6462, (if (_server_name!="") then {_server_name} else {_server_uniqueid})]
	[6470, _versionString                                      ] call FUNCTION_CTRLSETTEXT
	[6480, [_server_modfolders, "\n"] call FUNCTION_MODS2STRING] call FUNCTION_CTRLSETTEXT
	[6490, _server_maxcustomfilesize call FUNCTION_CUSTOM_SIZE ] call FUNCTION_CTRLSETTEXT
	[6510, _voice_name                                         ] call FUNCTION_CTRLSETTEXT
	[6520, [_server_languages, ", "] call FUNCTION_ARRAY2STRING] call FUNCTION_CTRLSETTEXT
	[6530, _server_location                                    ] call FUNCTION_CTRLSETTEXT
	[6540, _domain select 3                                    ] call FUNCTION_CTRLSETTEXT
	[6550, _server_message                                     ] call FUNCTION_CTRLSETTEXT
	
	; Verify game version
	[6472, _server_version != _my_game_version] call FUNCTION_SHOW_RED_TEXT

	; Verify modfolders
	_missing_mods_id        = []
	_missing_mods_name      = []
	_missing_mods_assign    = []
	_missing_mods_version   = []
	_missing_mods_myversion = []
	_missing_mods_forcename = []
	_missing_mods_sizearray = [0,0,0]
	_missing_mods_size      = ""
	FUNCTION_IS_MOD_MISSING forEach _server_modfolders

	[6482, count _missing_mods_id > 0] call FUNCTION_SHOW_RED_TEXT
	[6492, _too_big] call FUNCTION_SHOW_RED_TEXT

	; Verify game times
	_i 			= 0
	_playingNow = _server_uniqueid in GS_PLAYING_NOW

	_server_status_name = if (count _server_status > 0) then {MAINMENU_STR select 107} else {MAINMENU_STR select 106}
	ctrlSetText [6464, Format ["%1               %2", _server_status_name, ["@d M 0h:0i",_now] call FLIB_FORMATDATE]]

	; Display playing times
	? count _server_game_times2 == 0 : ctrlShow [6500, false]
	[6502, !_playingNow] call FUNCTION_SHOW_RED_TEXT
	ctrlSetText [6501, ""]
	"ctrlSetText [6501, (ctrlText 6501) + (_x select 0) + ""\n""]" forEach _server_game_times2

		; Estimate download size
		_roundToTenths = "private [""_fraction""];_this=_this-(_this mod 0.001); _fraction=(_this mod 0.01);_this=_this-_fraction;if (_fraction>0.004999) then {_this=_this+0.01};_fraction=(_this mod 0.1);_this=_this-_fraction;if (_fraction>0.049999) then {_this=_this+0.1};_this"
		_index         = 0
		while "_index<count _missing_mods_sizearray  &&  (_missing_mods_sizearray select _index)==0" do "_index=_index+1"
		? count _missing_mods_id > 0 : _missing_mods_size=_missing_mods_sizearray select _index; if (_index<2) then {_missing_mods_size=_missing_mods_size+(_missing_mods_sizearray select (_index+1))/1024}; _missing_mods_size=Format ["%1 %2", _missing_mods_size call _roundToTenths, ["GB","MB","KB"] select _index]

	? _server_uniqueid == _jump_to_server : _jump_to_server=""; goto "ServerOptions"
	_jump_to_server = ""
	goto "ResetLMB"



	; Display detailed information about a server
	#DisplayServerStatus
	"ctrlShow [_x,false]" forEach _info_window
	"ctrlShow [_x,true ]" forEach [6460, 6463, 6464]
	
	? _data != _server_uniqueid : _server_status=[]; call loadFile Format ["\:IGSE DB file:..\fwatch\tmp\schedule\dl_schedule.db read:%1", _data]
	? count _server_status == 0 : ctrlSetText [6463, Format ["%1: %2", localize "STR_STATUS", MAINMENU_STR select 106]]; goto "ResetLMB"
	
	ctrlSetText [6463, Format ["%1: %2\n%3 %4\n%5 %6\n%7 %8\n\n%9", localize "STR_STATUS", (_server_status select 0) call FUNCTION_GET_SERVER_STATUS_NAME, localize "STR_DISP_SERVER_MISSION", (_server_status select 2), localize "STR_DISP_SERVER_ISLAND", (_server_status select 3), localize "STR_DISP_MP_PLAYERS", (_server_status select 1), (_server_status select 4)]]
	goto "ResetLMB"



	; Double-click on the server name - show options
	#ServerOptions
	_run_voice_program = false
	_voice_format = ""
	? _voice_name != "" : _voice_format=if ((GS_VOICE select (_server_voice select 0)) select 1) then {MAINMENU_STR select 12} else {MAINMENU_STR select 11}
	
	lbClear 6657
	lbSetCurSel [6657, -1]
	? _server_version in [0,_my_game_version] && count _missing_mods_id==0 : comment "autoconnect;connect"; if (!_playingNow) then {[MAINMENU_STR select 26, 230, _color_yellowgreen] call FUNCTION_LBADD}; [MAINMENU_STR select 27, 221, _color_yellowgreen] call FUNCTION_LBADD
	? count _missing_mods_id > 0                                           : comment "download;changelog"; [Format [MAINMENU_STR select 28, _missing_mods_size], 204, _color_fireenginered] call FUNCTION_LBADD; lbSetValue [6657, lbAdd [6657, MAINMENU_STR select 29], 224]
	? _voice_name != ""                                                    : comment "voip"; lbSetValue [6657, lbAdd [6657, Format [_voice_format,_voice_name]], 222]
	? count _missing_mods_id==0 && count _server_modfolders > 0            : comment "changelog"; lbSetValue [6657, lbAdd [6657, Format [MAINMENU_STR select 29, _missing_mods_size]], 225]
	? _server_website != ""                                                : comment "website"; lbSetValue [6657, lbAdd [6657, MAINMENU_STR select 30], 205]
	? _server_version == _my_game_version                                  : comment "parameters"; lbSetValue [6657, lbAdd [6657, MAINMENU_STR select 31], 206]
	
	lbSetValue [6657, lbAdd [6657, MAINMENU_STR select 32], 207]
	~0.1
	goto "ResetLMB"
	
	#Voice
	? _voice_name == "" : goto "ExecuteProgram"
	? (GS_VOICE select (_server_voice select 0) select 1) : goto "ExecuteProgram"
	
	lbClear 6657
	lbSetCurSel [6657, -1]
	lbSetValue [6657, lbAdd [6657, Format [MAINMENU_STR select 33,_voice_name]], 217]
	lbSetValue [6657, lbAdd [6657, Format [MAINMENU_STR select 34,_voice_name]], 218]
	lbSetValue [6657, lbAdd [6657, MAINMENU_STR select 32], 212]
	~0.1
	goto "ResetLMB"
	
	#ExecuteProgram
	? _action == "auto"    : goto "ScheduleGameRestart"
	? _action == "connect" : loadFile ("\:RESTART CLIENT "+ (call FUNCTION_GET_EXECUTE_PARAMS)); closeDialog 0; exit
	goto "ResetLMB"




	; Download mods that the server requires
	#DownloadModfolders
	"ctrlShow [_x,false]" forEach _info_window
	"ctrlShow [_x,true ]" forEach [6460, 6463]	

	; check if mods we want to install are loaded
	_i = 0
	_loaded_mods = ""
	_loaded_mods_count = 0
	{if ([_x,FWATCH_CURRMOD] call FUNCTION_FIND>=0) then {_loaded_mods_count=_loaded_mods_count+1; _loaded_mods=Format["%1%2%3",_loaded_mods, (if (_i==0) then {""} else {", "}), _x]; _i=_i+1}} forEach _missing_mods_name
	? _i == 0 : goto "AssignID"

	_format_str = if (_loaded_mods_count > 1) then {MAINMENU_STR select 77} else {MAINMENU_STR select 76}

	ctrlSetText [6463, Format [_format_str, _loaded_mods]]
	lbClear 6657
	lbSetCurSel [6657, -1]
	lbSetValue  [6657, lbAdd [6657, MAINMENU_STR select 6] , 1]
	lbSetValue  [6657, lbAdd [6657, MAINMENU_STR select 36], 210]
	goto "ResetLMB"
	

	; if we can assign id to a mod instead of downloading then ask for it
	#AssignID
	? !(_missing_mods_assign select _i) : goto "AssignID_NextMod"
	ctrlSetText [6463, Format [MAINMENU_STR select 37, _missing_mods_name select _i, _missing_mods_version select _i]]
	lbClear 6657
	lbSetCurSel [6657, -1]
	lbSetValue  [6657, lbAdd [6657, MAINMENU_STR select 38], 214]
	lbSetValue  [6657, lbAdd [6657, MAINMENU_STR select 39], 213]
	lbSetValue  [6657, lbAdd [6657, MAINMENU_STR select 36], 210]
	goto "ResetLMB"

	#AssignID_ForThisMod
	_ok = call loadFile Format ["\:EXE ADDONINSTALL  -assignidpath=%1 -assignname=%1 -assignid=%2;%3 -assignkeepname=%4", _missing_mods_name select _i, _missing_mods_id select _i, _missing_mods_version select _i, _missing_mods_forcename select _i]
	? !(_ok select 0) : titleText [Format [MAINMENU_STR select 40,_ok select 3], "PLAIN DOWN", 1.5]; goto "ServerOptions"
	_pid = _ok select 4
	
		#AssignID_ForThisMod_Wait
		~0.01
		_ok = call loadFile Format ["\:EXE ADDONINSTALL check %1", _pid]
		? _ok select 0 : goto "AssignID_ForThisMod_Wait"
		
	_missing_mods_id        set [_i, "<null>"]
	_missing_mods_name      set [_i, "<null>"]
	_missing_mods_assign    set [_i, "<null>"]
	_missing_mods_version   set [_i, "<null>"]
	_missing_mods_myversion set [_i, "<null>"]
	_missing_mods_forcename set [_i, "<null>"]
	_missing_mods_sizearray set [_i, "<null>"]

	#AssignID_NextMod
	? _i < count _missing_mods_id-1 : _i=_i+1; goto "AssignID"
	_missing_mods_id        = _missing_mods_id        - ["<null>"]
	_missing_mods_name      = _missing_mods_name      - ["<null>"]
	_missing_mods_assign    = _missing_mods_assign    - ["<null>"]
	_missing_mods_version   = _missing_mods_version   - ["<null>"]
	_missing_mods_myversion = _missing_mods_myversion - ["<null>"]
	_missing_mods_forcename = _missing_mods_forcename - ["<null>"]
	_missing_mods_sizearray = _missing_mods_sizearray - ["<null>"]
	~0.1
	? count _missing_mods_id == 0 : _value=210; goto "Option"


	; Prepare info for the installer
	_ok = call loadFile ("\:IGSE WRITE  open:recreate  file:..\fwatch\tmp\schedule\installurl.txt  text:"+(_url+"?mode=install"+(["modinstall",_missing_mods_id,_missing_mods_myversion] call FUNCTION_BUILD_QUERY_STRING)))
	? !(_ok select 0) : titleText [Format [MAINMENU_STR select 41,_ok select 3], "PLAIN DOWN", 0.5]; goto "DisplayServer"
	if (_playingNow) then {_auto_restart=true; loadFile Format ["\:IGSE WRITE  open:recreate  file:..\fwatch\tmp\schedule\InstallerInstruction.txt  text:restart"]} else {loadFile "\:IGSE NEW  mode:delete  file:..\fwatch\tmp\schedule\InstallerInstruction.txt"}
	
	; Start installation program
	_run_voice_program = false
	? _voice_name != "" : _run_voice_program=!(GS_VOICE select (_server_voice select 0) select 1)
	
	_result = loadFile ("\:EXE ADDONINSTALL  "+(call FUNCTION_GET_EXECUTE_PARAMS)+Format[" -gameversion=%1 -language=%2 -downloadscript=fwatch\tmp\schedule\installurl.txt ", _my_game_version,CURRENT_LANGUAGE]+(call FUNCTION_WRITE_INSTALL))
	_ok     = call _result
	? !(_ok select 0) : titleText [Format [MAINMENU_STR select 40,_ok select 3], "PLAIN DOWN", 1.5]; goto "Schedule"
	
	_pid            = _ok select 4
	_install_status = 0
	_progress       = ""
	_progress_upd   = false
	? Format ["%1",_pid] == "scalar bool array string 0xfcffffef" : titleText [Format [MAINMENU_STR select 40,_result], "PLAIN", 1.5]; goto "Schedule"

	; Save variables indicating that installation started
	_result = call loadFile Format ["\:IGSE DB  file:..\fwatch\tmp\schedule\schedule.bin  key:savestatewrite:_pid=%1;_server_name=""%2"";_server_equalModReq=%3;_server_uniqueid=""%4"";_voice_name=""%5""", _pid,(if (_server_name!="") then {_server_name} else {_server_uniqueid}),_server_equalModReq,_server_uniqueid,_voice_name]

		#InstallDisplay
		"ctrlShow [_x,false]" forEach _info_window
		"ctrlShow [_x,true ]" forEach [6460, 6463]

		lbClear 6657
		lbSetCurSel [6657, -1]
		? _install_status == 0 : lbSetValue  [6657, lbAdd [6657, MAINMENU_STR select 112], 234]
		? _install_status == 3 : lbSetValue  [6657, lbAdd [6657, "["+(localize "STR_CAMPAIGN_CONTINUE")+"]"], 235]
		lbSetValue  [6657, lbAdd [6657, MAINMENU_STR select 36], 208]
		lbSetValue  [6657, lbAdd [6657, Format [MAINMENU_STR select (if (_server_uniqueid=="mod") then {42} else {88}), (if (_auto_restart) then {MAINMENU_STR select 43} else {MAINMENU_STR select 44})]], 209]

		? _auto_restart && _voice_name!=""         : if (!(GS_VOICE select (_server_voice select 0) select 1)) then {lbSetValue [6657, lbAdd [6657, Format ["[%1: %2]", _voice_name, (if (_run_voice_program) then {MAINMENU_STR select 43} else {MAINMENU_STR select 44})]], 219]}
		? _auto_restart && _server_uniqueid!="mod" : lbSetValue  [6657, lbAdd [6657, MAINMENU_STR select 31], 206]

		lbSetValue  [6657, lbAdd [6657, ""], -1]
		lbSetValue  [6657, lbAdd [6657, MAINMENU_STR select 85], 227]

		_current_auto_restart = _auto_restart
		_current_run_voice    = _run_voice_program

		; Wait for the program to finish
		#InstallWait
		_progressText = loadFile "\:IGSE LOAD  mode:execute  file:..\fwatch\tmp\schedule\install_progress.sqf"
		if (count (call loadFile ("\:STRING FIND find:""text:"+_progressText)) == 2) then {_progress=call _progressText; _progress_upd=false}
		? _progress == "" : _progress=MAINMENU_STR select 10
		? !(_auto_restart in [_current_auto_restart])   : goto "InstallDisplay"
		? !(_run_voice_program in [_current_run_voice]) : goto "InstallDisplay"

		? _install_status == 0 : if (lbSize 6657 == 0) then {goto "InstallDisplay"}; _ok=call loadFile Format ["\:EXE ADDONINSTALL  check %1", _pid]; _install_hanged=!(_ok select 0); if (_install_hanged && !_progress_upd) then {_progress_upd=true; if ((_ok select 2) in [740,186]) then {_progress=_progress+Format ["\n\n%1 %2 - %3\n\nRight click on fwatch.exe --> Properties --> Compatibility --> Run this program as an administrator",MAINMENU_STR select 63,(_ok select 2),(_ok select 3)]} else {_progress=_progress+(MAINMENU_STR select 45)}}
		ctrlSetText [6463, _progress]

		; waiting for user input
		? _install_status == 1  &&  lbSize 6657 > 0 : lbClear 6657; lbSetCurSel [6657,-1]
		? _install_status == 2  &&  lbValue [6657,0]!=233  &&  !_install_madechoice : lbClear 6657; lbSetCurSel [6657,-1]; lbSetValue [6657, lbAdd [6657, "["+(localize "STR_DISP_INT_RETRY")+"]"], 233]; lbSetValue  [6657, lbAdd [6657, MAINMENU_STR select 36], 208]
		? _install_status != 2 : _install_madechoice=false
		? _install_status == 3 && lbValue[6657,0]==234 : goto "InstallDisplay"
		? _install_status == 0 && lbValue[6657,0]==235 : goto "installDisplay"
		
		; installer finished
		? _install_status > 3 && lbSize 6657 > 1 : lbClear 6657; lbSetCurSel [6657,-1]; lbSetValue [6657, lbAdd [6657, MAINMENU_STR select 32], 210]
		goto "MainLoop"
		
	#CloseInstaller
	_ok = call loadFile Format ["\:EXE ADDONINSTALL  close %1", _pid]
	_jump_to_server = _server_uniqueid
	_install_status = -1
	loadFile ":IGSE DB  file:..\fwatch\tmp\schedule\schedule.bin  remove:savestate"
	loadFile "\:IGSE NEW  mode:delete  file:..\fwatch\tmp\schedule\install_progress.sqf"
	call FUNCTION_REFRESH_MODLIST
	if (_server_uniqueid=="mod") then {goto "ModLauncher_Display"} else {goto "Schedule"}
	



	; Extra startup parameters input
	#EditStartupParameters
	_Input      = ""
	call loadFile Format ["\:IGSE DB  file:..\fwatch\tmp\schedule\params.bin  read:%1", _server_uniqueid]
	_Label      = "SaveStartupParameters"
	_Input_Note = if (_server_equalModReq) then {MAINMENU_STR select 46} else {""}
	goto "INPUT"

	#SaveStartupParameters
	_Input = loadFile Format ["\:STRING TRIM  left:1  right:1  text:%1", _Input]
	call loadFile Format ["\:IGSE DB  file:..\fwatch\tmp\schedule\params.bin  key:%1write:_Input=""%2""", _server_uniqueid, _Input]
	goto "ResetLMB"


	; Schedule connection to the server
	#Schedule_ListServerEvents
	lbClear 6657
	lbSetCurSel [6657, -1]
	_i=0; FUNCTION_LIST_EVENTS_FOR_AUTO_CONNECTION forEach _server_game_times2
	lbSetValue [6657, lbAdd [6657, MAINMENU_STR select 32], 212]
	goto "ResetLMB"
	
	#ScheduleGameRestart
	_steam            = false
	_fwatch_info      = call loadFile "\:IGSE LOAD  mode:execute  file:..\fwatch_info.sqf"
	? Format ["%1",_fwatch_info] != "scalar bool array string 0xfcffffef" : _steam="-steam" in (_fwatch_info select 1)
	
	_event_recurrence = ((_server_game_times2 select _selected_event) select 2) select 0
	_event_date       = ((_server_game_times2 select _selected_event) select 2) select 1
	_event_id         = (_server_game_times2 select _selected_event) select 1
	_new_url = _url + Format["?mode=gamerestart&server=%1&eventid=%2",_server_uniqueid, _event_id] + ([_access_code, "password"] call FUNCTION_FORMAT_PASSWORD_STRING)
	_ok = call loadFile Format ["\:RESTART SCHEDULE eventid:%1 date:%2 recurrence:%3 comment:%4parameters:-eventvoice=%5 -eventurl=%6 -steam=%7", _event_id, _event_date, _event_recurrence, _server_name, (if (_run_voice_program) then {1} else {0}), _new_url, _steam]

	if (_ok select 0) then {titleText [localize "STR_MISSION_COMPLETED", "PLAIN DOWN", 0.1]} else {titleText [_ok select 3, "PLAIN DOWN", 1.5]}
	_scheduled_events = (call loadFile Format ["\:RESTART SCHEDULE mode:list"]) select 4
	goto "Schedule_ListServerEvents"

	#CancelGameRestart
	_selected_event = call _data
	_event_id       = (_server_game_times2 select _selected_event) select 1
	_ok             = call loadFile Format ["\:RESTART SCHEDULE mode:delete eventid:%1", _event_id]
	
	if (_ok select 0) then {titleText [localize "STR_MISSION_COMPLETED", "PLAIN DOWN", 0.1]} else {titleText [_ok select 3, "PLAIN DOWN", 1.5]}
	_scheduled_events = (call loadFile Format ["\:RESTART SCHEDULE mode:list"]) select 4
	goto "Schedule_ListServerEvents"



	; Save passwords for private games
	#SaveAccessCode
	;_Input       = loadFile Format ["\:STRING TRIM  left:1  right:1  text:%1", _Input]
	_access_code = _Input
	loadFile Format ["\:IGSE DB  file:..\fwatch\tmp\schedule\params.bin  key:accesscodewrite:_access_code=""%1""", _Input]
	@!(ctrlVisible 6901)
	goto "Schedule"



	; Save passwords for private mods
	#SaveModAccessCode
	;_Input               = loadFile Format ["\:STRING TRIM  left:1  right:1  text:%1", _Input]
	_access_code_mod     = _Input
	loadFile Format ["\:IGSE DB  file:..\fwatch\tmp\schedule\params.bin  key:accesscodemodwrite:_access_code_mod=""%1""", _Input]
	@!(ctrlVisible 6901)
	goto "DownloadMods"



	; Save mod search phrase
	#SaveModSearchPhrase
	;_Input      = loadFile Format ["\:STRING TRIM  left:1  right:1  text:%1", _Input]
	_mod_search_phrase = _Input
	loadFile Format ["\:IGSE DB  file:..\fwatch\tmp\schedule\params.bin  key:modsearchwrite:_mod_search_phrase=""%1""", _Input]
	@!(ctrlVisible 6901)
	goto "DownloadMods_ShowOptions"


	; Display information about patch
	#DisplayUpdateInformation
	_name         = MAINMENU_STR select 80
	_update_name  = "Fwatch"
	_option_value = 216
	? Format ["%1", count GS_FWATCH_LAST_UPDATE] == "scalar" : GS_FWATCH_LAST_UPDATE=GS_FWATCH_MY_VERSION
	_new = ["@Y MM d 0h:0i",GS_FWATCH_LAST_UPDATE] call FLIB_FORMATDATE
	_old = ["@Y MM d 0h:0i",GS_FWATCH_MY_VERSION] call FLIB_FORMATDATE
	? _my_cpp_version != _required_cpp_version : _name=MAINMENU_STR select 81; _new=_required_cpp_version; _old=_my_cpp_version; _option_value=226; _update_name="Resource.cpp"
	
	lbClear 6657
	lbSetCurSel [6657, -1]
	[Format [MAINMENU_STR select 48, _update_name], _option_value, _color_fireenginered] call FUNCTION_LBADD
	lbSetValue  [6657, lbAdd [6657, MAINMENU_STR select 85], 227]
	
	"ctrlShow [_x,false]" forEach _info_window
	"ctrlShow [_x,true ]" forEach [6460, 6463]
	ctrlSetText [6463, Format [MAINMENU_STR select 49, _name, _new, _old]]
	goto "ResetLMB"









	; Code that is reused (but not a function because I use delay) ======================================================
	#INPUT
	createDialog "MAINMENU_INPUT"
	ctrlShow [6900, true]
	ctrlSetText [6900, MAINMENU_STR select 74]
	ctrlSetText [6901, _Input]
	ctrlSetText [6902, _Input_Note]
	ctrlEnable  [6902, false]
	
	MAINMENU_INPUT = true
	_clip_size     = 0
	_enter         = false

		#InputLoop
		~0.01
		? !dialog || !ctrlVisible 6901 : goto "InputEnd"
		
		_keys = FWATCH_INPUT_MULTI select 0
		? "ENTER" in _keys   : _enter=true; goto "InputEnd"
		? "RBUTTON" in _keys : closeDialog 0
		
		_Input = ctrlText 6901
		goto "InputLoop"
		
		
	#InputEnd
	@!("ENTER" in (FWATCH_INPUT_MULTI select 0))
	? ctrlVisible 6901 : closeDialog 0
	MAINMENU_INPUT = false
	? !_enter : goto "ResetLMB"
	goto _Label









	; Download image, display it if possible, save info to database =====================================================
	#DownloadImageThread
	_img_folder   = _this select 1
	_database     = _this select 2
	_url          = _this select 3
	_record_id    = _this select 4
	_global_hash  = _this select 5
	_record_title = _this select 6
	_cursel       = _this select 7
	
	_extension = loadFile Format ["\:STRING CUT start:-3 text:%1", _url]
	? _extension!="jpg"  &&  _extension!="paa" &&  _extension!="pac" : exit
	
	GS_DOWNLOAD_RESULT = []
	["--tries=1 "+_url, Format["schedule\%1\%2.%3", _img_folder, _record_id, _extension], "GS_DOWNLOAD_RESULT"] exec "..\fwatch\data\Download.sqs"
	@count GS_DOWNLOAD_RESULT != 0

	? !(GS_DOWNLOAD_RESULT select 0) : exit
	loadFile Format ["\:IGSE DB  file:..\fwatch\tmp\schedule\%1  key:%2write:_extension={%3};_logohash={%4};", _database, _record_id, _extension, _global_hash]
	? dialog  &&  ctrlText 6462==_record_title  &&  lbCurSel 6657==_cursel : ctrlSetText [6461, Format["..\fwatch\tmp\schedule\%1\%2.%3", _img_folder, _record_id, _extension]]
	exit









	; Download game schedule database with serverds/mods ================================================================	
	#DownloadDatabase
	? _silent_mode : goto "DownloadDatabase_SkipDisplay"
	lbClear 6657
	lbSetCurSel [6657, -1]
	(MAINMENU_STR select 10) call FUNCTION_MSG_LB
	"ctrlShow [_x,false]" forEach _info_window
	ctrlSetText [6463, ""]
	#DownloadDatabase_SkipDisplay

		; Read save state
		_pid                = -1
		_install_status     = -1
		_server_name        = ""
		_server_uniqueid    = ""
		_server_equalModReq = false
		_auto_restart       = false
		_voice_name         = ""
		_run_voice_program  = false
		_progress           = ""
		_progress_upd       = false
		_playingNow         = false
		call loadFile "\:IGSE DB  file:..\fwatch\tmp\schedule\schedule.bin  read:savestate"
		call loadFile "\:IGSE LOAD  mode:execute  file:..\fwatch\tmp\schedule\install_progress.sqf"
		call loadFile "\:IGSE LOAD  mode:execute  file:..\fwatch\tmp\schedule\restart_info.sqf"
		? _pid!=-1 : _ok=call loadFile Format ["\:EXE ADDONINSTALL  check %1", _pid]; if (_ok select 0) then {if (_silent_mode) then {_progress=call loadFile "\:IGSE LOAD  mode:execute  file:..\fwatch\tmp\schedule\install_progress.sqf"; titleText [_progress, "PLAIN", 0.1]; exit} else {goto "InstallDisplay"}} else {_pid=-1; _install_status=-1; loadFile ":IGSE DB  file:..\fwatch\tmp\schedule\schedule.bin  remove:savestate"; loadFile "\:IGSE NEW  mode:delete  file:..\fwatch\tmp\schedule\install_progress.sqf"}
	
	; Silent mode - download only once per game launch
	_now = call loadFile ":info date"
	? !_silent_mode : goto "DownloadDatabase_SkipSilentCheck"
	_last_silent_mode = []
	call loadFile "\:IGSE DB  file:..\fwatch\tmp\schedule\schedule.bin  read:silentmode"
	? count _last_silent_mode > 0 : if ([_last_silent_mode,_now,"fraction"] call FLIB_DATEDIFFDAY < 0.0014) then {goto "SilentMode_Display"}
	loadFile Format ["\:IGSE DB  file:..\fwatch\tmp\schedule\schedule.bin  key:silentmodewrite:_last_silent_mode=%1;", _now]
	#DownloadDatabase_SkipSilentCheck
	
	_url              = ""
	_access_code      = ""
	_access_code_mod  = ""
	_update_available = false
	_saved_modhash    = ""
	_saved_version    = 0

	; Create folder
	_ok = call loadFile Format ["\:IGSE NEW  directory:true  file:..\fwatch\tmp\schedule"]
	? !(_ok select 0) : titleText [Format[MAINMENU_STR select 13,_ok select 3],"PLAIN",1]; closeDialog 0; exit
	_ok = call loadFile Format ["\:IGSE NEW  directory:true  file:..\fwatch\tmp\schedule  mode:check"]
	? !(_ok select 0) : titleText [Format[MAINMENU_STR select 13,_ok select 3],"PLAIN",1]; closeDialog 0; exit
	_ok = call loadFile Format ["\:IGSE NEW  directory:true  file:..\fwatch\tmp\schedule\logo_server"]
	_ok = call loadFile Format ["\:IGSE NEW  directory:true  file:..\fwatch\tmp\schedule\logo_mod"]

	; Read saved data
	call loadFile "\:IGSE DB  file:..\fwatch\tmp\schedule\params.bin  read:accesscoderead:accesscodemod"
	call loadFile "\:IGSE DB  file:..\fwatch\tmp\schedule\schedule.bin  read:version"
	? _saved_version < GS_MY_VERSION : 	loadFile "\:IGSE NEW  mode:delete  file:..\fwatch\tmp\schedule\schedule.bin"
	call loadFile "\:IGSE DB  file:..\fwatch\tmp\schedule\schedule.bin  read:generalread:hash"

	_i          = 0
	_downloaded = 0
	_mirror     = 0

		#DownloadDatabase_ForEachURL
		_url = (GS_DEFAULT_URL select _i) select _mirror
		? !_silent_mode : ((call loadFile Format ["\:STRING DOMAIN url:%1", _url]) select 3) call FUNCTION_MSG_LB
		
		; Download timestamp and compare it against local timestamp
		? _mode == "schedule" : goto "DownloadDatabase_Full"
		
		_last_modified = "";
		call loadFile Format ["\:IGSE DB file:..\fwatch\tmp\schedule\dl_%1.db read:timestamp", _mode]
		? _last_modified == "" : goto "DownloadDatabase_Full"

		? !_silent_mode : (MAINMENU_STR select 14) call FUNCTION_MSG_LB
		GS_DOWNLOAD_RESULT = []
		[Format ["--verbose --output-file=fwatch\tmp\schedule\downloadlog.txt --tries=1 https://ofp-faguss.com/schedule/sqf.php?mode=mods_v2_last_modified",_mode,CURRENT_LANGUAGE,_now select 8], Format["schedule\dl_%1.sqf",_mode], "GS_DOWNLOAD_RESULT"] exec "..\fwatch\data\Download.sqs"
		@count GS_DOWNLOAD_RESULT != 0
		? !(GS_DOWNLOAD_RESULT select 0) : if (_mirror < count (GS_DEFAULT_URL select _i)-1) then {_mirror=_mirror+1; goto "DownloadDatabase_ForEachURL"} else {goto "DownloadDatabase_Done"}
		_result = call loadFile Format["..\fwatch\tmp\schedule\dl_%1.sqf",_mode]
		? Format ["%1",_result] != "true" : goto "DownloadDatabase_Full"

		if (Format ["%1",count GS_FWATCH_LAST_UPDATE]!="scalar") then {_update_available=([GS_FWATCH_LAST_UPDATE,GS_FWATCH_MY_VERSION,"fraction","subtract"] call FLIB_DATEDIFFDAY) > 0; if (_update_available) then {goto "DownloadDatabase_Done"}}
		? GS_VERSION != GS_MY_VERSION : [MAINMENU_STR select 20,""] call FUNCTION_DOWNLOAD_INFO; goto "ResetLMB"
		
		if (GS_LAST_MODIFIED==_last_modified && _saved_modhash==FWATCH_MODLISTHASH) then {goto "DownloadDatabase_Done"}
		
		; Download full info
		#DownloadDatabase_Full
		? _mode=="schedule" && Format["%1",GS_SCHEDULE_DOWNLOAD_TIME]!="scalar bool array string 0xfcffffef" : if (time<GS_SCHEDULE_DOWNLOAD_TIME) then {goto "DownloadDatabase_Read"}
		
		? !_silent_mode : (MAINMENU_STR select 16) call FUNCTION_MSG_LB
		GS_DOWNLOAD_RESULT = []
		[Format ["--verbose --output-file=fwatch\tmp\schedule\downloadlog.txt --tries=1 https://ofp-faguss.com/schedule/sqf.php?mode=%1_v2&server=current&language=%2&timeoffset=%3",_mode,CURRENT_LANGUAGE,_now select 8]+(["allusermods"] call FUNCTION_BUILD_QUERY_STRING), Format["schedule\dl_%1.db",_mode], "GS_DOWNLOAD_RESULT"] exec "..\fwatch\data\Download.sqs"
		@count GS_DOWNLOAD_RESULT != 0
		? !(GS_DOWNLOAD_RESULT select 0) : if (_mirror < count (GS_DEFAULT_URL select _i)-1) then {_mirror=_mirror+1; goto "DownloadDatabase_ForEachURL"} else {goto "DownloadDatabase_Done"}
		? _mode == "schedule" : GS_SCHEDULE_DOWNLOAD_TIME=time+10

		? !_silent_mode : (MAINMENU_STR select 17) call FUNCTION_MSG_LB
		
		#DownloadDatabase_Read
		["DownloadDatabase_ForEachURL", Format["schedule\dl_%1.db",_mode]] call FUNCTION_READ_DOWNLOADED_FILE

		if (Format ["%1",count GS_FWATCH_LAST_UPDATE]!="scalar") then {_update_available=([GS_FWATCH_LAST_UPDATE,GS_FWATCH_MY_VERSION,"fraction","subtract"] call FLIB_DATEDIFFDAY) > 0; if (_update_available) then {goto "DownloadDatabase_Done"}}
		? GS_VERSION != GS_MY_VERSION : [MAINMENU_STR select 20,""] call FUNCTION_DOWNLOAD_INFO; goto "ResetLMB"

		; Save info
		loadFile (Format ["\:IGSE DB  file:..\fwatch\tmp\schedule\schedule.bin  key:generalwrite:_update_available=%1;GS_FWATCH_LAST_UPDATE=%2;_url=""%3"";GS_VOICE=",_update_available,GS_FWATCH_LAST_UPDATE,_url] + (GS_VOICE call FLIB_FORMAT))
		loadFile Format ["\:IGSE DB  file:..\fwatch\tmp\schedule\schedule.bin  key:versionwrite:_saved_version=%1", GS_MY_VERSION]
		? _mode=="mods" : loadFile Format ["\:IGSE DB  file:..\fwatch\tmp\schedule\schedule.bin  key:hashwrite:_saved_modhash=""%1""", FWATCH_MODLISTHASH]
		goto "DownloadDatabase_Done"
	
		#DownloadDatabase_ForEachURL_Skip
		? _i < count GS_DEFAULT_URL-1 : _i=_i+1; _mirror=0; goto "DownloadDatabase_ForEachURL"
		
	#DownloadDatabase_Done
	? _update_available : if (_silent_mode) then {titleText [MAINMENU_STR select 86,"PLAIN DOWN",1]; exit} else {goto "DisplayUpdateInformation"}
	? _silent_mode : goto "SilentMode_Display"
	
	_ok = call loadFile Format ["\:IGSE DB file:..\fwatch\tmp\schedule\dl_%1.db read:generalread:listbox", _mode]
	? !(_ok select 0) : [MAINMENU_STR select 18,_ok select 3] call FUNCTION_DOWNLOAD_INFO; goto "ResetLMB"
	
	; Update mods __gs_id files if necessary
	_j = 0
	? count GS_MODS_TO_VERIFY == 0 : goto "VerifyModfolders_Next"
	
	#VerifyModfolders
	_index_local = [(GS_MODS_TO_VERIFY select _j) select 0, FWATCH_MODLISTID] call FUNCTION_FIND
	? _index_local < 0 : goto "VerifyModfolders_Next"
	; oldforcename in [newforcename] and newname==oldname
	? ((FWATCH_MODLISTCFG select _index_local) select 3) in [(GS_MODS_TO_VERIFY select _j) select 2] && ((GS_MODS_TO_VERIFY select _j) select 1)==((FWATCH_MODLISTCFG select _index_local) select 2) : goto "VerifyModfolders_Next"
	
	; Rewrite id file
	(FWATCH_MODLISTCFG select _index_local) set [2, (GS_MODS_TO_VERIFY select _j) select 1]
	(FWATCH_MODLISTCFG select _index_local) set [3, (GS_MODS_TO_VERIFY select _j) select 2]
	loadFile Format ["\:EXE ADDONINSTALL  -assignidpath=%1 -assignname=%2 -assignid=%3;%4 -assignkeepname=%5", FWATCH_MODLIST select _index_local, (GS_MODS_TO_VERIFY select _j) select 1, FWATCH_MODLISTID select _index_local, (FWATCH_MODLISTCFG select _index_local) select 0, (GS_MODS_TO_VERIFY select _j) select 2]
	#VerifyModfolders_Next
	? _j < count GS_MODS_TO_VERIFY-1 : _j=_j+1; goto "VerifyModfolders"
	GS_MODS_TO_VERIFY = nil
	
	? _mode == "schedule" : goto "Schedule_List_Servers"
	? _mode == "mods" : goto "DownloadMods_List"
	exit
	
	#SilentMode_Display
	_player_count     = 0
	_mod_update_count = 0
	call loadFile Format ["\:IGSE DB file:..\fwatch\tmp\schedule\dl_%1.db read:modupdatecountread:playercount", _mode]
	
	? _mode == "mods" : if (_mod_update_count>0) then {titleText [Format [MAINMENU_STR select 87, _mod_update_count], "PLAIN DOWN", 0.2]}; _mode="schedule"; goto "DownloadDatabase_SkipSilentCheck"
	? _mode == "schedule" : if (_player_count>0) then {cutText [Format ["\n%1 %2", localize "STR_DISP_MP_PLAYERS", _player_count], "PLAIN DOWN", 0.2]}
	